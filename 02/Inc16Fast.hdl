/**
 * Fast 16-bit incrementer.
 */

CHIP Inc16Fast {
	IN in[16];
	OUT out[16];

	PARTS:
	/* stage1 */
	And(a=in[0], b=in[1], out=Ones0to1);
	And(a=in[2], b=in[3], out=Ones2to3);
	And(a=in[4], b=in[5], out=Ones4to5);
	And(a=in[6], b=in[7], out=Ones6to7);
	And(a=in[8], b=in[9], out=Ones8to9);
	And(a=in[10], b=in[11], out=Ones10to11);
	And(a=in[12], b=in[13], out=Ones12to13);
	// And(a=in[14], b=in[15], out=Ones14to15);

	/* stage2 */
	And(a=Ones0to1, b=in[2], out=Ones0to2);
	And(a=Ones4to5, b=in[6], out=Ones4to6);
	And(a=Ones8to9, b=in[10], out=Ones8to10);
	And(a=Ones12to13, b=in[14], out=Ones12to14);

	And(a=Ones0to1, b=Ones2to3, out=Ones0to3);
	And(a=Ones4to5, b=Ones6to7, out=Ones4to7);
	And(a=Ones8to9, b=Ones10to11, out=Ones8to11);
	// And(a=Ones12to13, b=Ones14to15, out=Ones12to15);

	/* stage3 */
	And(a=Ones0to3, b=in[4], out=Ones0to4);
	And(a=Ones0to3, b=Ones4to5, out=Ones0to5);
	And(a=Ones0to3, b=Ones4to6, out=Ones0to6);

	And(a=Ones8to11, b=in[12], out=Ones8to12);
	And(a=Ones8to11, b=Ones12to13, out=Ones8to13);
	And(a=Ones8to11, b=Ones12to14, out=Ones8to14);

	And(a=Ones0to3, b=Ones4to7, out=Ones0to7);
	// And(a=Ones8to11, b=Ones12to15, out=Ones8to15);

	/* stage4 */
	And(a=Ones0to7, b=in[8], out=Ones0to8);
	And(a=Ones0to7, b=Ones8to9, out=Ones0to9);
	And(a=Ones0to7, b=Ones8to10, out=Ones0to10);
	And(a=Ones0to7, b=Ones8to11, out=Ones0to11);
	And(a=Ones0to7, b=Ones8to12, out=Ones0to12);
	And(a=Ones0to7, b=Ones8to13, out=Ones0to13);
	And(a=Ones0to7, b=Ones8to14, out=Ones0to14);
	// And(a=Ones0to7, b=Ones8to15, out=Ones0to15);

	/* stage5 */
	Not(in=in[0], out=out[0]);
	Xor(a=in[1], b=in[0], out=out[1]);
	Xor(a=in[2], b=Ones0to1, out=out[2]);
	Xor(a=in[3], b=Ones0to2, out=out[3]);
	Xor(a=in[4], b=Ones0to3, out=out[4]);
	Xor(a=in[5], b=Ones0to4, out=out[5]);
	Xor(a=in[6], b=Ones0to5, out=out[6]);
	Xor(a=in[7], b=Ones0to6, out=out[7]);
	Xor(a=in[8], b=Ones0to7, out=out[8]);
	Xor(a=in[9], b=Ones0to8, out=out[9]);
	Xor(a=in[10], b=Ones0to9, out=out[10]);
	Xor(a=in[11], b=Ones0to10, out=out[11]);
	Xor(a=in[12], b=Ones0to11, out=out[12]);
	Xor(a=in[13], b=Ones0to12, out=out[13]);
	Xor(a=in[14], b=Ones0to13, out=out[14]);
	Xor(a=in[15], b=Ones0to14, out=out[15]);
}
